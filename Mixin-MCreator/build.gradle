import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id("java")
    id("java-library")
}

group = "org.liquid"
version = "1.0.0"

repositories {
    mavenCentral()
    maven { url "https://repo.gradle.org/gradle/libs-releases" } // for gradle-tooling-api
}

dependencies {
    implementation project(':MCreator')

    project(":MCreator").configurations.named("implementation").get().dependencies.each {
        //noinspection ForeignDelegate
        implementation it
    }

}

processResources {
    eachFile { details ->
        if (details.name == "plugin.json") { // or whatever pattern/criteria is appropriate
            details.filter ReplaceTokens, tokens: [
                    "mainPlugins.version": version,
                    "minMCr"             : minMCr,
                    "maxMCr"             : maxMCr
            ], beginToken: "{", endToken: "}"
        }
    }
}

tasks.withType(org.gradle.jvm.tasks.Jar).configureEach {
    archiveFileName.set("mixin.zip")
    doLast {
        copy {
            from 'build/libs/mixin.zip'
            into mcreator_plugin_path
        }
    }
}